buildscript {
    ext.gaeVersion = '1.9.38'
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.google.appengine:gradle-appengine-plugin:$gaeVersion"
    }
}

apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'appengine'

repositories {
    jcenter()
}

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.6'
    compile 'com.github.groovy-wslite:groovy-wslite:1.1.2'
    compile 'org.gaelyk:gaelyk:2.1.2'

    compile "com.google.appengine:appengine-api-1.0-sdk:$gaeVersion"
    compile "com.google.appengine:appengine-api-labs:$gaeVersion"
    compile 'javax.servlet:servlet-api:2.5'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'org.gaelyk:gaelyk-spock:0.4'
    testCompile "com.google.appengine:appengine-api-stubs:$gaeVersion"
    testCompile "com.google.appengine:appengine-testing:$gaeVersion"

    appengineSdk "com.google.appengine:appengine-java-sdk:$gaeVersion"
}

war {
    webAppDirName = "$buildDir/exploded-app"
}

appengine {
    jvmFlags << '-Xdebug'
    jvmFlags << '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8890'
    jvmFlags << "-Ddatastore.backing_store=$buildDir/local_db.bin"
    downloadSdk = true
    disableUpdateCheck = true
    appcfg {
        oauth2 = true
        if (file("$buildDir/appengine-key.json").exists()) {
            extraOptions << "--service_account_json_key_file=$buildDir/appengine-key.json"
        }
    }
}

task appengineWebApp(type: Sync, dependsOn: jar) {
    into "$buildDir/exploded-app/WEB-INF"
    from 'src/main/config'
    filesMatching('appengine-web.xml') {
        filter { line ->
            line.replace('@VERSION@', System.getenv('CIRCLE_BRANCH') ?: 'default')
        }
    }
    into('lib') { from configurations.runtime, jar.archivePath }
    into('groovy') { from 'src/main/groovlet' }
}

tasks.war.enabled = false
tasks.appengineExplodeApp.enabled = false
tasks.appengineExplodeApp.dependsOn appengineWebApp
